
plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 35
    namespace "com.tencent.qcloud.tuikit.tuicallkit"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def callSourceJavaVersion = JavaVersion.VERSION_24

    compileOptions {
        sourceCompatibility callSourceJavaVersion
        targetCompatibility callSourceJavaVersion
    }
}

// Add this block at the top level of your build.gradle file
// (outside the android block)
kotlin {
    jvmToolchain(24) // Or your desired Java version, as an integer
}

dependencies {
    api fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    api 'androidx.appcompat:appcompat:1.7.1'
    api 'com.google.android.material:material:1.12.0'
    api 'androidx.recyclerview:recyclerview:1.4.0'
    api 'androidx.constraintlayout:constraintlayout:2.2.1'
    api "com.google.code.gson:gson:2.13.1"

    def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()
    if (projects.contains("tuicore")) {
        api project(':tuicore')
    } else {
        api "com.tencent.imsdk:imsdk-plus:8.6.7019"
        api "com.tencent.liteav.tuikit:tuicore:8.6.7020"
    }
    if (projects.contains("engine_source")) {
        api project(':engine_source')
    } else {
        api rootProject.getProperties().containsKey("liteavSdk") ? rootProject.ext.liteavSdk : "com.tencent.liteav:LiteAVSDK_Professional:12.6+"
        api rootProject.getProperties().containsKey("roomEngineSdk") ? rootProject.ext.roomEngineSdk : "io.trtc.uikit:rtc_room_engine:3.2.+"
    }
    if (projects.contains("common")) {
        api project(':common')
    } else {
        api 'io.trtc.uikit:common:3.2.0.1016'
    }
}
